import com.github.spotbugs.SpotBugsTask

plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE"
    id 'java'
    id 'jacoco'
    id "com.github.spotbugs" version "3.0.0"
    id "com.moowork.node" version "1.3.1"
}


node {
    version = '20.11.0'
    npmVersion = '10.2.4'
    download = true
    workDir = file("${project.buildDir}/plugin/ui")
    nodeModulesDir = file("${project.rootDir}/plugin/ui")
}

npm_run_build {
    inputs.files fileTree("ui/src")
    inputs.file 'ui/package.json'
    inputs.file 'ui/package-lock.json'
    outputs.dir 'ui/build'
}

apply from: 'project-properties.gradle'
apply from: scriptsUrl + '/release-fat.gradle'
apply from: scriptsUrl + '/signing.gradle'

repositories {
    if (!releaseMode) {
        maven { url 'https://jitpack.io' }
    }
    mavenLocal()
    mavenCentral { url "https://repo1.maven.org/maven2" }
}

dependencyManagement {
    imports {
        mavenBom(releaseMode ? 'com.epam.reportportal:commons-bom:' + getProperty('bom.version') : 'com.epam.reportportal:commons-bom:5.12.1')
    }
}

dependencies {
    if (releaseMode) {
        implementation 'com.epam.reportportal:commons-dao'
        implementation 'com.epam.reportportal:plugin-api'
        annotationProcessor 'com.epam.reportportal:plugin-api'
    } else {
        implementation 'com.github.reportportal:commons-dao:acf1ec7'
        implementation 'com.github.reportportal:commons:c8ef09c'
        implementation 'com.github.reportportal:plugin-api:188792e'
        annotationProcessor 'com.github.reportportal:plugin-api:188792e'
    }
    implementation('org.hibernate:hibernate-core:5.4.18.Final')
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.7'
    compile 'org.apache.commons:commons-compress:1.27.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.2'
    testImplementation "org.mockito:mockito-junit-jupiter:3.4.6"
}

spotbugs {
    sourceSets = [sourceSets.main]
    reportLevel = "high"
}
tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

test {
    useJUnitPlatform()
    maxParallelForks = 1
    testLogging {
        events = ['failed']
        exceptionFormat = 'short'
    }
    reports {
        junitXml.enabled = true
    }
}

build.dependsOn jacocoTestReport

artifacts {
    archives shadowJar
}

generatePomFileForShadowPublication { pom.packaging = "jar" }

sourceSets {
    main {
        resources
                {
                    exclude '**'
                }
    }
}

jar {
    from("src/main/resources") {
        into("/resources")
    }
    from("ui/build") {
        into("/resources")
    }
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(' '),
                "Plugin-Id": "${pluginId}",
                "Plugin-Version": "${project.version}",
                "Plugin-Provider": "Report Portal",
                "Plugin-Class": "com.epam.reportportal.extension.importing.ImportXUnitPlugin",
                "Plugin-Service": "api"
        )
    }
}

shadowJar {
    from("src/main/resources") {
        into("/resources")
    }
    from("ui/build") {
        into("/resources")
    }
    configurations = [project.configurations.compile]
    zip64 true
    dependencies {
    }
}

task plugin(type: Jar) {
    getArchiveBaseName().set("plugin-${pluginId}")
    into('classes') {
        with jar
    }
    into('lib') {
        from configurations.compile
    }
    extension('zip')
}

task assemblePlugin(type: Copy) {
    from plugin
    into pluginsDir
}

task assemblePlugins(type: Copy) {
    dependsOn subprojects.assemblePlugin
}

//compileJava.dependsOn npm_run_build

